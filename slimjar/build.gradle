import java.nio.file.Files
import java.nio.file.Paths

plugins {
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'java'
    id 'maven-publish'
}

version '1.1.2-LOCAL'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation 'me.lucko:jar-relocator:1.4'
    testImplementation 'com.google.code.gson:gson:2.8.6'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.1.0'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation 'cglib:cglib:3.1'
}

publishing {
    publications {
        mavenPublish(MavenPublication) {
            from components.java
            groupId 'io.github.slimjar'
            artifactId 'slimjar'
            version project.version
            pom {
                name = "SlimJar"
                description = "A simple and robust runtime dependency manager for JVM languages."
                url = "http://www.github.com/SlimJar/slimjar"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id = "vshnv"
                        name = "Vaishnav Anil"
                        email = "vaishnavanil7th@gmail.com"
                    }
                    developer {
                        id = "ipsk"
                        name = "Mateus Moreira"
                    }
                }
                scm {
                    connection = "https://github.com/SlimJar/slimjar"
                    developerConnection = "https://github.com/SlimJar/slimjar.git"
                    url = "https://github.com/SlimJar/slimjar"
                }
            }

        }
    }
    repositories {
        maven {
            url "https://repo.vshnv.tech/"
            credentials {
                username = project.findProperty("reposiliteAlias") ?: ""
                password = project.findProperty("reposiliteToken") ?: ""
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

tasks.register('copyExternal') {
    it.dependsOn(':slimjar-external:jar')
    final File externalJar = project(':slimjar-external').getTasks().getByName("jar").outputs.files.singleFile
    println(externalJar)
    println(externalJar.exists())

    if (!externalJar.exists()) return
    final File resourceModule = layout.buildDirectory.file("resources/main/slimjar-external.isolated-jar").get().asFile
    resourceModule.getParentFile().mkdirs()
    if (resourceModule.exists()) {
        resourceModule.delete()
    }
    Files.copy(externalJar.toPath(), resourceModule.toPath())
}

shadowJar {
    dependsOn('copyExternal')
}